apply plugin: 'com.android.application'
apply plugin: 'com.readdle.android.swift'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

swift {
    useKapt true
    cleanEnabled true
    debug {
        extraBuildFlags("-Xswiftc", "-DDEBUG")
    }
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.readdle.swiftjava.sample"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["com.readdle.codegen.package": """{
                   "moduleName": "SampleProject",
                   "importPackages": ["SampleAppCore"],
                   "customTypeMappings": {
                           "com.readdle.swiftjava.sample.CustomSampleValue": "SampleValue"
                   }
               }
               """
                ]
            }
        }
    }
    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            shrinkResources false
            ndk {
                abiFilters = ["armeabi-v7a", "arm64-v8a", "x86", "x86_64"]
            }
        }
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            shrinkResources false
            ndk {
                abiFilters = ["x86", "x86_64"]
            }
        }
    }

    // Configure only for each module that uses Java 8
    // language features (either in its source code or
    // through dependencies).
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xuse-experimental=kotlin.ExperimentalUnsignedTypes'
    }
}

dependencies {
    kapt project(':compiler')
    implementation project(':library')

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
