apply plugin: 'com.android.application'
apply plugin: 'com.readdle.android.swift'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

swift {
    useKapt true
    cleanEnabled true
    debug {
        extraBuildFlags("-Xswiftc", "-DDEBUG")
    }
}

android {
    compileSdkVersion 34
    namespace = "com.readdle.swiftjava.sample"
    compileSdkVersion 34
    defaultConfig {
        applicationId "com.readdle.swiftjava.sample"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["com.readdle.codegen.package": """{
                   "moduleName": "SampleProject",
                   "importPackages": ["SampleAppCore"],
                   "customTypeMappings": {
                           "com.readdle.swiftjava.sample.CustomSampleValue": "SampleValue"
                   }
               }
               """
                ]
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            shrinkResources false
        }
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            shrinkResources false
        }
    }
}


tasks.withType(Exec).configureEach { t ->
    if (t.name.startsWith("swiftBuild")) {
        environment "ANDROID_NDK_HOME", System.getenv("ANDROID_NDK_HOME")
        environment "ANDROID_NDK", System.getenv("ANDROID_NDK")
        // Optionally force destination file
        args "--destination", "${System.getenv("HOME")}/.android-swift/swift-android/build-tools/src/bash/aarch64-unknown-linux-android29.json"
    }
}

dependencies {
    kapt project(':compiler')
    implementation project(':library')

    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
